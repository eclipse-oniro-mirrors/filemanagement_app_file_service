# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/filemanagement/app_file_service/app_file_service.gni")
import("//foundation/filemanagement/app_file_service/backup.gni")

part_name = "app_file_service"
subsystem_name = "filemanagement"
taihe_generated_file_path =
    "$taihe_file_path/out/$subsystem_name/$part_name/backup"

copy_taihe_idl("copy_taihe") {
  sources = [ "${app_file_service_path}/interfaces/kits/taihe/backup/idl/ohos.file.backup.taihe" ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.file.backup.ani.cpp",
    "$taihe_generated_file_path/src/ohos.file.backup.abi.c",
  ]
}

taihe_shared_library("file_backup_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "${app_file_service_path}/interfaces/common/include",
    "${app_file_service_path}/interfaces/common/include/log.h",
    "${app_file_service_path}/interfaces/kits/taihe/backup/include",
    "${app_file_service_path}/interfaces/inner_api/native/backup_kit_inner/impl",
    "${app_file_service_path}/utils/include/",
  ]

  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/general_callbacks_taihe.cpp",
    "src/ohos.file.backup.impl.cpp",
  ]

  deps = [
    ":run_taihe",
    "${path_backup}/interfaces/inner_api/native/backup_kit_inner:backup_kit_inner",
    "${path_backup}/utils:backup_utils",
  ]

  external_deps = [
    "ability_base:zuri",
    "c_utils:utils",
    "file_api:filemgmt_libhilog",
    "file_api:filemgmt_libn",
    "hilog:libhilog",
    "ipc:ipc_core",
    "napi:ace_napi",
    "samgr:samgr_proxy",
  ]
}

generate_static_abc("file_backup_taihe_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.file.backup.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/file_backup_taihe_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("file_backup_etc") {
  source = "$target_out_dir/file_backup_taihe_abc.abc"
  module_install_dir = "framework"
  part_name = "app_file_service"
  subsystem_name = "filemanagement"
  deps = [ ":file_backup_taihe_abc" ]
}

group("afs_backup_taihe") {
  deps = [
    ":file_backup_etc",
    ":file_backup_taihe",
  ]
}
