import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/filemanagement/app_file_service/app_file_service.gni")

part_name = "app_file_service"
subsystem_name = "filemanagement"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name/fileuri"

copy_taihe_idl("copy_taihe") {
  sources = [ "${app_file_service_path}/interfaces/kits/taihe/fileuri/idl/ohos.file.fileuri.taihe" ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.file.fileuri.ani.cpp",
    "$taihe_generated_file_path/src/ohos.file.fileuri.abi.c",
  ]
}

taihe_shared_library("file_fileuri_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  include_dirs = [
    "include",
    "${app_file_service_path}/interfaces/common/include",
    "${app_file_service_path}/interfaces/common/include/log.h",
    "${app_file_service_path}/interfaces/innerkits/native/file_uri/include",
    "${app_file_service_path}/interfaces/kits/ndk/fileshare/include"
  ]

  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/fileuri_taihe.cpp"
  ]

  deps = [
    ":run_taihe",
    "${app_file_service_path}/interfaces/innerkits/native:fileuri_native",
  ]

  external_deps = [
    "hilog:libhilog",
    "ability_base:zuri",
  ]
}

generate_static_abc("file_fileuri_taihe_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.file.fileuri.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/file_fileuri_taihe_abc.abc"
  dependencies = [ ":run_taihe" ]
}

generate_static_abc("file_fileuri_test") {
  base_url = "${app_file_service_path}/interfaces/kits/taihe/fileuri/test"
  files = [
    "${app_file_service_path}/interfaces/kits/taihe/fileuri/test/test_main.ets",
  ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/file_fileuri_test.abc"
}

ohos_prebuilt_etc("file_fileuri_etc") {
  source = "$target_out_dir/file_fileuri_taihe_abc.abc"
  module_install_dir = "framework"
  part_name = "app_file_service"
  subsystem_name = "filemanagement"
  deps = [
    ":file_fileuri_taihe_abc",
  ]
}

group("afs_fileuri_taihe") {
  deps = [
    ":file_fileuri_taihe",
    ":file_fileuri_etc",
  ]
}