# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/es2panda/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/filemanagement/app_file_service/backup.gni")

es2abc_gen_abc("gen_backup_extension_ability_abc") {
  src_js = rebase_path("backup_extension_ability.js")
  dst_file = rebase_path(target_out_dir + "/backup_extension_ability.abc")
  in_puts = [ "backup_extension_ability.js" ]
  out_puts = [ target_out_dir + "/backup_extension_ability.abc" ]
  extra_args = [ "--module" ]
}

gen_js_obj("backup_extension_ability_js") {
  input = "backup_extension_ability.js"
  output = target_out_dir + "/backup_extension_ability.o"
}

gen_js_obj("backup_extension_ability_abc") {
  input = get_label_info(":gen_backup_extension_ability_abc",
                         "target_out_dir") + "/backup_extension_ability.abc"
  output = target_out_dir + "/backup_extension_ability_abc.o"
  dep = ":gen_backup_extension_ability_abc"
}

# 必须命名为backupextensionability或backupextensionability_napi
# 在将@ohos.application.BackupExtensionAblity翻译为库名时，只是将.作为目录分割符，然后将库名转换为小写后拼接.z.so或_napi.z.so后进行查找
# 即，尝试加载/system/lib64/application/backupextensionability.z.so或/system//lib64/application/backupextensionability_napi.z.so
# 具体请参考//foundation/arkui/napi/module_manager/native_module_manager.cpp
ohos_shared_library("backupextensionability_napi") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  deps = [
    ":backup_extension_ability_abc",
    ":backup_extension_ability_js",
    "${path_backup}/frameworks/js/backup_ext:backupextensionability_napi_fwk",
  ]

  relative_install_dir = "module/application"
  part_name = "app_file_service"
  subsystem_name = "filemanagement"
}
